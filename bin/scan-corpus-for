#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Corpus;
use Term::ReadKey;

die "usage: $0 query\n" unless @ARGV;

ReadMode 3;
END { ReadMode 0 }

my $corpus = Anki::Corpus->new;

sub copy {
    my $text = shift;
    open my $handle, '| pbcopy';
    print $handle $text;
    close $handle;
    say "Copied: $text";
}

my %commands = (
    "s" => sub { shift->suspend },
    "u" => sub { shift->unsuspend},
    "q" => sub { ReadMode 0; exit },
    "N" => sub { copy(shift->japanese) },
    "S" => sub { copy(shift->source) },
    "H" => sub { copy(shift->translation) },
    "Y" => sub { copy(shift->readings) },
);
my $commands = join '', sort keys %commands;

my $cb = sub {
    my %args     = @_;
    my $sentence = $args{sentence};
    my $regex    = $args{regex};
    my $index    = $args{index};
    my $count    = $args{count};

    while (1) {
        $corpus->print_sentence($sentence, $regex);

        local $commands{j} = sub {
            last;
        } if ${ $args{next} } < $count;

        my $back_one = $index - 1;
        local $commands{k} = sub {
            ${ $args{next} } = $back_one;
            last;
        } if $back_one >= 0;

        delete local $commands{H} unless $sentence->has_translation;
        delete local $commands{Y} unless $sentence->has_readings;
        delete local $commands{$sentence->suspended ? 's' : 'u'};

        my $commands = join '', sort keys %commands;
        local $| = 1;
        print "[".($index+1)."/$count] Command [$commands]: ";

        my $command = ReadKey 0;
        do { ReadMode 0; exit } if !defined($command);
        print "$command\n";
        last if $command eq "\n" || $command eq " ";

        if (!exists($commands{$command})) {
            say "Invalid command $command";
            next;
        }

        $commands{$command}->($sentence);
        $sentence->refresh;
    }
};

my $count = $corpus->scan_for([@ARGV], $cb, 'WHERE suspended=1');

if (!$count) {
    say "No suspended results, showing unsuspended rows";
    $corpus->scan_for([@ARGV], $cb, 'WHERE suspended=0');
}

