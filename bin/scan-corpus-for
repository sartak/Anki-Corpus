#!/usr/bin/env perl
use 5.14.0;
use warnings;
no warnings 'exiting';
use utf8::all;
use Anki::Corpus;
use Term::ReadKey;

die "usage: $0 query\n" unless @ARGV;

ReadMode 3;
END { ReadMode 0 }

my $current_search = qr/./;
my $corpus = Anki::Corpus->new;
chomp(my $paste = `which paste-into-anki.scpt`);

sub copy {
    my $text = shift || '';
    open my $handle, '| pbcopy';
    print $handle $text;
    close $handle;
    say "Copied: $text" if $text;
}

# THIS IS SO INCREDIBLY FRAGILE
sub add_to_anki {
    my $sentence = shift;

    my $add_field = sub {
        my $text = shift;
        copy($text);
        system("osascript $paste");
    };

    my $source = $sentence->source;

    if ($sentence->source eq 'Twitter') {
        my $author = $sentence->note('作者');
        $source = $sentence->note('URL')
               || ($author ? ('@' . $author) : $source);
    }

    $add_field->($sentence->japanese);    # 日本語
    $add_field->('');                     # 前後関係
    $add_field->('');                     # 定義
    $add_field->($sentence->translation); # 翻訳
    $add_field->($sentence->readings);    # 読み
    $add_field->($source);                # 出所
}

my %commands = (
    "s" => {
        description => 'suspend sentence',
        code        => sub { shift->suspend }
    },
    "u" => {
        description => 'unsuspend sentence',
        code        => sub { shift->unsuspend },
    },
    "d" => {
        description => 'delete sentence',
        code        => sub { shift->delete; last },
    },
    "q" => {
        description => 'cut out all this nonsense',
        code        => sub { ReadMode 0; exit },
    },
    "a" => {
        description => 'add to Anki',
        code        => sub { add_to_anki(shift) },
    },
    "N" => {
        description => 'copy sentence (日本語)',
        code        => sub { copy(shift->japanese) },
    },
    "S" => {
        description => 'copy source (出所)',
        code        => sub { copy(shift->source) },
    },
    "H" => {
        description => 'copy translation (翻訳)',
        code        => sub { copy(shift->translation) },
    },
    "Y" => {
        description => 'copy readings (読み)',
        code        => sub { copy(shift->readings) },
    },
    "e" => {
        description => 'edit sentence',
        code        => sub {
            my $sentence = shift;
            local $| = 1;

            while (1) {
                my %triggers = (
                    n => 'japanese',
                    t => 'translation',
                    s => 'source',
                    r => 'readings',
                );

                for my $trigger (sort keys %triggers) {
                    my $method = $triggers{$trigger};
                    say "[$trigger] $method: " . ($sentence->$method // '(none)');
                }
                print "Edit which field: ";
                my $key = ReadKey 0;
                return if !defined($key) || $key eq 'q' || $key eq "\n";

                say $key;

                my $method = $triggers{$key};

                if (!$method) {
                    say "Invalid trigger '$key'";
                    next;
                }

                say "Current: " . ($sentence->$method // '(none)');
                print "New:     ";
                ReadMode 0;
                my $new = scalar readline STDIN;
                chomp $new;
                ReadMode 3;

                return if !defined($new) || $new eq '';

                $corpus->dbh->do("update sentences set $method=? where rowid=?", {}, $new, $sentence->rowid);

                $sentence->refresh;
            }
        },
    },
    '/' => {
        description => 'search',
        code        => sub {
            local $| = 1;
            print "Search (currently /$current_search/): ";
            ReadMode 0;
            my $new_search = scalar readline STDIN;
            chomp $new_search;
            ReadMode 3;

            $new_search = '.' if $new_search eq '';
            $current_search = qr/$new_search/;
        },
    },
);
my $commands = join '', sort keys %commands;

my $cb = sub {
    my %args     = @_;
    my $sentence = $args{sentence};
    my $regex    = $args{regex};
    my $index    = $args{index};
    my $count    = $args{count};

    return unless $sentence->japanese =~ $current_search;

    while (1) {
        print "\n";
        $corpus->print_sentence($sentence, $regex);

        local $commands{j} = {
            description => 'down one',
            code        => sub {
                last;
            },
        } if ${ $args{next} } < $count;

        my $back_one = $index - 1;
        local $commands{k} = {
            description => 'up one',
            code        => sub {
                ${ $args{next} } = $back_one;
                last;
            },
        } if $back_one >= 0;

        delete local $commands{H} unless $sentence->has_translation;
        delete local $commands{Y} unless $sentence->has_readings;
        delete local $commands{$sentence->suspended ? 's' : 'u'};

        $commands{'?'} = {
            description => 'get help',
            code        => sub {
                for my $key (sort keys %commands) {
                    say "$key: $commands{$key}{description}";
                }
            },
        };

        my $commands = join '', sort keys %commands;
        local $| = 1;
        print "[".($index+1)."/$count] Command [$commands]: ";

        my $command = ReadKey 0;
        do { ReadMode 0; exit } if !defined($command);
        print "$command\n";
        last if $command eq "\n" || $command eq " ";

        if (!exists($commands{$command})) {
            say "Invalid command $command";
            next;
        }

        $commands{$command}{code}->($sentence);
        $sentence->refresh;
    }
};

my $count = $corpus->scan_for([@ARGV], $cb, 'WHERE suspended=1');

if (!$count) {
    say "No suspended results, showing unsuspended rows";
    $corpus->scan_for([@ARGV], $cb, 'WHERE suspended=0');
}

