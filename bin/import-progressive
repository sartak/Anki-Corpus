#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Encode qw/encode_utf8 decode_utf8/;
use Anki::Corpus;
use Proc::InvokeEditor;

my $word = shift
    or die "usage: $0 word\n";

my $corpus = Anki::Corpus->new;
my @sentences;
my @forking_sentence;
my @reading_sentence;
my @ok;

while (<STDIN>) {
    next unless /\S/;

    if (/［.*］/) {
        push @forking_sentence, $_;
        next;
    }

    if (/（.*）/) {
        push @reading_sentence, $_;
        next;
    }

    push @ok, $_;
}

if (@forking_sentence) {
    push @ok, split /\n/, decode_utf8(Proc::InvokeEditor->edit(encode_utf8(join "\n", map { join "", $_, $_ } @forking_sentence)));
}

if (@reading_sentence) {
    my @output;
    for (@reading_sentence) {
        /（(.*?)）/;
        push @output, "# $_", "【$1】";
    }

    my @input = split /\n/, decode_utf8(Proc::InvokeEditor->edit(encode_utf8(join "\n", @output)));

    while (my (undef, $readings) = splice @input, 0, 2) {
        my $raw = shift @reading_sentence;
        $raw =~ s/（(.*?)）//;

        push @ok, {
            raw      => $raw,
            readings => $readings,
        };
    }
}

for my $line (@ok) {
    my $raw = ref $line ? $line->{raw} : $line;
    next unless $raw =~ /\S/;

    my ($japanese, $translation) = split '｜', $raw;

    unless ($japanese && $translation) {
        warn "Oddly formatted progressive line: $raw\n";
        next;
    }

    for ($japanese, $translation) {
        s/^\s+//;
        s/\s+$//;
    }

    push @sentences, {
        japanese    => $japanese,
        translation => $translation,
        (ref $line ? (readings => $line->{readings}) : ()),
    };
}

while (my $sentence = shift @sentences) {
    my $clarification = $1 if $sentence->{japanese} =~ s/\s*〔(.+)〕$//;

    my $id = $corpus->add_sentence(
        source => 'プログレッシブ英和・和英中辞典',
        %$sentence,
    );
    if ($id) {
        $corpus->add_note(
            sentence => $id,
            type     => '見出し語',
            value    => $word,
        );
        if ($clarification) {
            $corpus->add_note(
                sentence => $id,
                type     => '説明',
                value    => $clarification,
            );
        }
    }
}
