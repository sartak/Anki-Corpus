#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Corpus;

my $corpus = Anki::Corpus->new;
my $sth = $corpus->prepare("
    SELECT source, suspended, count(japanese)
    FROM sentences
    GROUP BY source, suspended
;");
$sth->execute;

my %source;
while (my ($source, $suspended, $count) = $sth->fetchrow_array) {
    for my $source ($source, 'ALL') {
        $source{$source}{$suspended ? "s" : "u"} += $count;
        $source{$source}{c} += $count;
    }
}

for my $source (keys %source) {
    $source{$source}{p} = ($source{$source}{u}||0) / $source{$source}{c};
}

for my $source ('ALL', sort { $source{$b}{p} <=> $source{$a}{p} } grep { $_ ne 'ALL' } keys %source) {
    my $count       = $source{$source}{c} || 0;
    my $suspended   = $source{$source}{s} || 0;
    my $unsuspended = $source{$source}{u} || 0;
    my $percent     = $source{$source}{p} || 0;

    say sprintf '%3d%%  %s: %d (U:%d + S:%d)', 100*$percent, $source, $count, $unsuspended, $suspended;
}
